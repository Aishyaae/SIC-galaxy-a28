#include <WiFi.h>
#include <HTTPClient.h>
#include <time.h>

const char* ssid = "******";
const char* password = "*****";

const char* serverName = "https://script.google.com/macros/s/AKfycbxLiGaubUe4ZR9W1pc6dvOV7AHAzFWbAHWCPt_m2XbG9uNlS4BUZFrZbeaw4BWelUZ5/exec"; 

String sensorData = "";

void setup() {
  Serial.begin(115200); // Serial monitor
  Serial2.begin(9600, SERIAL_8N1, 16, 17); 

  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Connecting to WiFi...");
  }
  Serial.println("Connected to WiFi");

  configTime(0, 0, "pool.ntp.org", "time.nist.gov");
  Serial.println("Waiting for time");
  while (!time(nullptr)) {
    Serial.print(".");
    delay(1000);
  }
}

void loop() {
  if (Serial2.available()) {
    sensorData = "";
    while (Serial2.available()) {
      char c = (char)Serial2.read();
      if (isPrintable(c)) {
        sensorData += c;
      }
    }
    if (sensorData.length() > 0) {
      Serial.print("Received data: ");
      Serial.println(sensorData);
      sendToGoogleSheets(sensorData);
    } else {
      Serial.println("Received empty data, not sending.");
    }
  }
  delay(500);
}

void sendToGoogleSheets(String data) {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    http.begin(serverName);
    http.addHeader("Content-Type", "application/x-www-form-urlencoded");

    String httpRequestData = "data=" + data;
    int httpResponseCode = http.POST(httpRequestData);

    if (httpResponseCode > 0) {
      String response = http.getString();
      Serial.println(httpResponseCode);
      Serial.println(response);
    } else {
      Serial.print("Error on sending POST: ");
      Serial.println(httpResponseCode);
    }
    http.end();
  } else {
    Serial.println("WiFi Disconnected");
  }
}
